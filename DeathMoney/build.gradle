import java.text.SimpleDateFormat

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version '7.0.0'
}

def pluginVersion = "2.0"
group "io.github.galaxyvn"
version = pluginVersion

repositories {
    mavenCentral()
    // Spigot
    maven { url "https://repo.codemc.org/repository/nms/" }
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    // PlaceholderAPI
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // Vault
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly("org.spigotmc:spigot:1.16.5-R0.1-SNAPSHOT")
    compileOnly("me.clip:placeholderapi:2.10.10")
    compileOnly("com.github.MilkBowl:VaultAPI:1.7")
}

compileJava {
    options.encoding = "UTF-8"
}

shadowJar {
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} " +
                        "(${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} " +
                        "${System.properties['os.version']}"
        )
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
